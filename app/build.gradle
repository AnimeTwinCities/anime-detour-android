apply plugin: 'com.android.application'

android {
    def specifiedVersionCode = project.hasProperty("versionCode") ? project.getProperty("versionCode") as Integer : 3000
    def specifiedVersionName = project.hasProperty("versionName") ? project.getProperty("versionName") as String : "2.0-SNAPSHOT"

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "org.animetwincities.animedetour"
        targetSdkVersion 25
        versionCode specifiedVersionCode
        versionName specifiedVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            if (project.hasProperty("keystoreFile")) {
                storeFile file(project.getProperty("keystoreFile") as String)
                storePassword project.getProperty("keystorePassword") as String
                keyAlias project.getProperty("keystoreAlias") as String
                keyPassword project.getProperty("keyPassword") as String
            }
        }
    }
    buildTypes {
        debug {
            defaultConfig {
                minSdkVersion project.hasProperty("supportLevel") ? 16 : 21
            }
            applicationIdSuffix '.debug'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled project.hasProperty("supportLevel")
            debuggable true
        }
        release {
            if (project.hasProperty("keystoreFile")) {
                signingConfig signingConfigs.release
            }
            defaultConfig {
                minSdkVersion 16
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.6'

    compile 'com.google.firebase:firebase-core:10.2.0'
    compile 'com.google.firebase:firebase-crash:10.2.0'
    compile 'com.google.firebase:firebase-config:10.2.0'
    compile 'com.google.firebase:firebase-database:10.2.0'
    compile 'com.google.firebase:firebase-auth:10.2.0'
    compile "com.google.android.gms:play-services-base:10.2.0"
    compile "com.google.android.gms:play-services-maps:10.2.0"

    compile 'com.google.dagger:dagger:2.9'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.9'

    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'frankiesardo:icepick:3.1.0'
    provided 'frankiesardo:icepick-processor:3.1.0'

    compile 'com.github.InkApplications:android-logger:4.1.0'
    compile 'com.github.InkApplications:android-layout-injector:1.1.0'
    compile 'com.github.InkApplications:guava-stopwatch:21.0'
    compile 'com.github.InkApplications:android-application-lifecycle:1.0.1'
    compile 'com.github.InkApplications:android-simple-recycler-view:v1.0.2'

    compile 'com.facebook.fresco:fresco:1.1.0'
    compile "com.facebook.fresco:imagepipeline-okhttp3:1.1.0"

    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.8'

    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    compile('com.mikepenz:materialdrawer:5.8.2@aar') {
        transitive = true
    }

    debugCompile 'com.facebook.stetho:stetho:1.4.2'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.4.2'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'me.tatarka.retrolambda'
